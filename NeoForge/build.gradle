plugins {
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.moddev'
}

base {
	archivesName = "${mod_name}-neoforge-${minecraft_version}"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	maven {
		name = 'Parchment'
		url = 'https://maven.parchmentmc.org'
	}
}

neoForge {
	version = neoforge_version

	// Use Parchment mappings
	parchment {
		minecraftVersion = parchment_mc_version
		mappingsVersion = parchment_version
	}

	// Automatically enable NeoForge AccessTransformers if the file exists
	def atFile = file('src/main/resources/META-INF/accesstransformer.cfg')
	if (atFile.exists()) {
		accessTransformers.from(atFile)
	}

	mods {
		"${mod_id}" {
			sourceSet sourceSets.main
		}
	}

	runs {
		client {
			client()
			gameDirectory = project.file('run/client')
			programArguments.addAll '--username', 'Dev'

			loadedMods = [mods."${mod_id}"]
		}

		server {
			server()
			gameDirectory = project.file('run/server')
			programArguments.addAll '--nogui'

			loadedMods = [mods."${mod_id}"]
		}

		serverData {
            serverData()
			gameDirectory = project.file('run/data')
			programArguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

			loadedMods = [mods."${mod_id}"]
		}
	}
}

dependencies {
	// We compile sources directly
}

processResources {
	from project(":Common").sourceSets.main.resources

	filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
		expand([
			'version': project.version,
			'minecraft_version': minecraft_version,
			'mod_id': mod_id,
			'mod_name': mod_name
		])
	}
}

tasks.named('compileJava', JavaCompile).configure {
	source(project(":Common").sourceSets.main.allSource)
	options.encoding = 'UTF-8'
	options.release = 25
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = base.archivesName.get()
			from components.java
		}
	}
	repositories {
		maven {
			url = "file://" + System.getenv("local_maven")
		}
	}
}
